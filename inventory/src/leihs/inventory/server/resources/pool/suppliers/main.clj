(ns leihs.inventory.server.resources.pool.suppliers.main
  (:require
   [clojure.set]
   [honey.sql :refer [format] :rename {format sql-format}]
   [honey.sql.helpers :as sql]
   [leihs.inventory.server.middlewares.debug :refer [log-by-severity]]
   [leihs.inventory.server.middlewares.exception-handler :refer [exception-handler]]
   [leihs.inventory.server.utils.pagination :refer [create-pagination-response]]
   [leihs.inventory.server.utils.request :refer [query-params]]
   [next.jdbc.sql :as jdbc]
   [ring.middleware.accept]
   [ring.util.response :refer [response]]))

(def ERROR_GET_SUPPLIERS "Failed to get suppliers")

(def base-query
  (-> (sql/select :s.id :s.name :s.note)
      (sql/from [:suppliers :s])
      (sql/order-by :s.name)))

(defn get-by-id [tx id]
  (-> base-query
      (sql/where [:= :s.id id])
      sql-format
      (->> (jdbc/query tx))
      first))

(defn index-resources
  ([request]
   (try
     (let [search-term (-> request query-params :search)
           base-query (-> base-query
                          (cond-> search-term
                            (sql/where [:ilike :s.name (str "%" search-term "%")])))]

       (response (create-pagination-response request base-query nil)))

     (catch Exception e
       (log-by-severity ERROR_GET_SUPPLIERS e)
       (exception-handler request ERROR_GET_SUPPLIERS e)))))
